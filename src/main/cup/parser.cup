package ParserLexer;
import java_cup.runtime.*;
import SymbolTable.*;
import Utils.*;
import java.util.HashMap;
import java.util.Map;
import java.util.ArrayList;
import java.util.List;
import Semantic.*;

action code {:
    private SymbolTableManager symTabMgr = new SymbolTableManager();
    private Map<String, TabSymbol> symbols = new HashMap<String, TabSymbol>();
    private ArrayList<String> parameters = new ArrayList<String>();
    private ArrayList<String> symList = new ArrayList<String>();
:}

parser code {:
    // Connect this parser to a scanner!
    lexer lex;

    @SuppressWarnings("deprecation")
    public parser(lexer lex){
        this.lex=lex;
        this.symbolFactory = new DefaultSymbolFactory();
    }

    int paramCount = 1;

    public final String ANSI_RESET = "\u001B[0m";
    public final String ANSI_RED = "\u001B[31m";

    // Metodo para reportar los errores sintacticos, pero funciona con ayuda de Recuperación en Modo Pánico + producciones de la gramatica
    public void syntax_error(Symbol s){
        System.out.println(Utils.ANSI_RED + "syntax_error: Error sintáctico '"+ s.value + "' en la línea: " + s.left + ", columna: " + s.right + Utils.ANSI_RESET);
    }

    // Metodo para reportar los errores irreparables
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        System.out.println(Utils.ANSI_RED + "unrecovered_syntax_error: Error sintáctico inrecuperable '"+ s.value + "' en la línea: "+ s.left + ", columna: " + s.right + Utils.ANSI_RESET);
    }

:};

/* define how to connect to the scanner! */
init with {: :};
scan with {: return lex.next_token(); :};

/* Terminales (tokens retornados por el scanner). */
// Lexema Separador ( , )
terminal PINO;
// Operadores Aritméticos Binarios ( +, -, *, /, %, ** ):
terminal DASHER, DANCER, PRANCER, VIXEN, COMET, RUDOLPH;
// Operadores Aritméticos Unarios ( ++, -- ):
terminal GRINCH, QUIEN;
// Operadores Relacionales ( ==, !=, >, <, =<, =< ):
terminal ALABASTER, BUSHY, PEPPER, SUGARPLUM, WUNORSE, JINGLE;
// Operadores Logicos ( ^, #, ! ):
terminal MELCHOR, GASPAR, BALTAZAR;
// Tipos de Datos ( char, int, float, boolean, string ):
terminal SANTACLAUS, FATHERCHRISTMAS, KRISKRINGLE, SANNICOLAS, DEDMOROZ;
// Literales
terminal L_SANTACLAUS; // Nuestro scanner provee l_SANTACLAUS como char
terminal L_FATHERCHRISTMAS; // Nuestro scanner provee l_SANTACLAUS como int
terminal L_KRISKRINGLE; // Nuestro scanner provee l_SANTACLAUS como float
terminal L_SANNICOLAS; // Nuestro scanner provee l_SANTACLAUS como boolean
terminal L_DEDMOROZ; // Nuestro scanner provee l_SANTACLAUS como String
// Parentesis ( (, ), [, ], {, } ):
terminal ABRECUENTO, CIERRACUENTO, ABREEMPAQUE, CIERRAEMPAQUE, ABREREGALO, CIERRAREGALO;
// Lexemas de Estructuras de Control ( if, elif, else, for, do, until, return, break ):
terminal ELFO, HADA, DUENDE, ENVUELVE, HACE, REVISA, ENVIA, CORTA;
// Lexemas de Lectura/Escritura ( print, read ):
terminal NARRA, ESCUCHA;
// Lexema de Fin de Expresión ( | ):
terminal FINREGALO;
// Lexema de Asignación ( <= ):
terminal ENTREGA;
// Identificador ( variable, función ):
terminal PERSONA;
// EXTRA:
terminal LOCAL;
terminal FUNCTION;

// No terminales
nonterminal navidad; //Inicial
nonterminal block; // Bloque de código
nonterminal type, arr_type; //Tipos y tipos de arreglo
nonterminal func_decl, func_decl_list, var_decl, arr_decl; // Declaraciones
nonterminal param_list, param; // Parámetros
nonterminal var_decl_init, arr_decl_init; // Variables y arreglos inicializados
nonterminal stmt, init_stmt, expr_stmt, assg_stmt, ctrl_struct_stmt, input_output_stmt, return_stmt, break_stmt; // Declaraciones
nonterminal stmt_list; //Lista de declaraciones
nonterminal conditional_stmt, iter_stmt; // Estructuras de control
nonterminal elif_else;
nonterminal do_until_loop, for_loop;
nonterminal read_stmt, write_stmt;
nonterminal expr, expr_list; // Expresiones
nonterminal and_expr, not_expr, rel_expr, rel_op;
nonterminal add_sub_expr, add_sub_ops;
nonterminal mul_div_expr, mul_div_ops;
nonterminal exp_expr;
nonterminal un_arith_expr, un_arith_ops;
nonterminal factor;
nonterminal mutable, immutable; // Mutable o inmutable
nonterminal call; // Llamada a función
nonterminal args, arg_list; // Argumentos
nonterminal literal; // Literales
nonterminal gen_id, arr_id, identifier, var_init, arr_init;
start with navidad;

/**********************************************************************************************************************/

navidad ::= func_decl_list {: symTabMgr.printAllTables(); :};

func_decl_list ::= func_decl
                 | func_decl func_decl_list {: :};

func_decl ::= FUNCTION identifier:id ABRECUENTO param_list CIERRACUENTO block {:
                         paramCount = 1;
                         RESULT = "function::" + (String)id;
                         Utils.splitAddFunc((String)RESULT, symbols, symTabMgr, new ArrayList<String>(parameters));
                         parameters.clear();:};

identifier ::= gen_id:id {: RESULT = (String)id; :}
             | arr_id:id {: RESULT = (String)id; :};

type ::= FATHERCHRISTMAS: t_int {: RESULT = (String)t_int; :}
       | KRISKRINGLE: t_flt {: RESULT = (String)t_flt; :}
       | SANTACLAUS: t_char {: RESULT = (String)t_char; :}
       | DEDMOROZ: t_str {: RESULT = (String)t_str; :}
       | SANNICOLAS: t_bool {: RESULT = (String)t_bool; :};

gen_id ::= type:tp PERSONA:id {: RESULT = (String)tp + "::" + (String)id; :};

arr_id ::= gen_id:id ABREEMPAQUE CIERRAEMPAQUE {: RESULT = (String)id + "::UND" ;  :}
         | gen_id:id ABREEMPAQUE expr:size CIERRAEMPAQUE {: RESULT = (String)id + "::" + (String)size; :};

param_list ::= param:par {: RESULT = (String)par; :}
             | param:par PINO param_list:par_l {: RESULT = (String)par + ", " + (String)par_l; :}
             | {: RESULT = ""; :};

param ::= identifier:id {: RESULT = "parameter::" + (String)id + "::" + paramCount;
                           paramCount++;
                           ArrayList<String> params = Utils.splitMessage((String)RESULT,"::");
                           parameters.add(params.get(1));
                           Utils.splitAddSym((String)RESULT, symbols); :};

block ::= ABREREGALO stmt_list CIERRAREGALO
        | error;

init_stmt ::= var_decl_init:var FINREGALO {: RESULT = "variable::" + (String)var;
                                             Utils.splitAddSym((String)RESULT, symbols); :}
            | arr_decl_init:arr FINREGALO {: RESULT = "array::" + (String)arr;
                                             Utils.splitAddSym((String)RESULT, symbols); :};

var_decl_init ::= var_init:var {: RESULT = (String)var; :}
                | var_decl:var {: RESULT = (String)var; :};

var_init ::= var_decl:var ENTREGA expr:expr {:
RESULT = (String)var + "::" + (String)expr;
symList = Utils.splitMessage((String)RESULT, "::");
ExpressionTree.checkExpressionType((String)expr, symList.get(0), symbols, symTabMgr);
:};

var_decl ::= LOCAL gen_id:id {: RESULT = (String)id; :};

arr_decl_init ::= arr_init:arr {: RESULT = (String)arr; :}
                | arr_decl:arr {: RESULT = (String)arr; :};

arr_init ::= arr_decl:id ENTREGA ABREEMPAQUE expr_list:expr_l CIERRAEMPAQUE {:
RESULT = (String)id + "::<ARRAY>" +(String)expr_l + "</ARRAY>";
System.out.println(expr_l);
symList = Utils.splitMessage((String)RESULT, "::");
ExpressionTree.checkExpressionType(symList.get(3), symList.get(0), symbols, symTabMgr);
:};

arr_decl ::= LOCAL arr_id:id {: RESULT = (String)id; :};

expr_list ::= expr:expr {: RESULT = "<EXPR>" + (String)expr + "</EXPR>";:}
            | expr:expr PINO expr_list:expr_l {: RESULT = "<EXPR>" + (String)expr + "</EXPR>" + (String)expr_l;:};

/**********************************************************************************************************************/

stmt ::= init_stmt
       | expr_stmt
       | assg_stmt
       | ctrl_struct_stmt
       | input_output_stmt
       | return_stmt
       | break_stmt;

stmt_list ::= stmt stmt_list
            | ;

expr_stmt ::= expr:expr FINREGALO {: RESULT = expr; :}
            | FINREGALO;

assg_stmt ::= mutable:mut ENTREGA expr:expr FINREGALO {: RESULT = (String)mut + "<=" + (String)expr ; :};

ctrl_struct_stmt ::= conditional_stmt
                   | iter_stmt;

conditional_stmt ::= ELFO ABRECUENTO expr CIERRACUENTO block elif_else;

elif_else ::= HADA ABRECUENTO expr CIERRACUENTO block elif_else
            | DUENDE block
            | ;

iter_stmt ::= do_until_loop
            | for_loop;

do_until_loop ::= HACE block REVISA ABRECUENTO expr CIERRACUENTO FINREGALO;

for_loop ::= ENVUELVE ABRECUENTO var_init FINREGALO expr FINREGALO expr CIERRACUENTO block
           | ENVUELVE ABRECUENTO assg_stmt expr FINREGALO expr CIERRACUENTO block;

input_output_stmt ::= read_stmt:stmt FINREGALO {: RESULT = stmt; :}
                    | write_stmt:stmt FINREGALO {: RESULT = stmt; :};

write_stmt ::= ESCUCHA:wrt ABRECUENTO PERSONA:id CIERRACUENTO {: RESULT = (String)wrt + "(" + (String)id + ")"; :};

read_stmt ::= NARRA:prnt ABRECUENTO expr:expr CIERRACUENTO {: RESULT = (String)prnt + "(" + (String)expr + ")"; :};

return_stmt ::= ENVIA:rtrn expr_stmt:expr_stmt {: RESULT = (String)rtrn + (String)expr_stmt; :};

break_stmt ::= CORTA:brk FINREGALO {: RESULT = (String)brk; :};

expr ::= and_expr:an_expr GASPAR:sym expr:expr {: RESULT = "<LOGIC><OP1>" + (String)an_expr + "</OP1><SYM>" +(String)sym + "</SYM><OP2>" + (String)expr + "</OP2></LOGIC>";  :}
       | and_expr:an_expr {: RESULT = (String)an_expr ;  :};

and_expr ::= not_expr:no_expr MELCHOR:sym and_expr:an_expr {: RESULT = "<LOGIC><OP1>" + (String)no_expr + "</OP1><SYM>"+ (String)sym + "</SYM><OP2>" + (String)an_expr + "</OP2></LOGIC>"; :}
           | not_expr:no_expr {: RESULT = (String)no_expr; :};

not_expr ::= BALTAZAR:sym rel_expr:re_expr {: RESULT = "<LOGIC><SYM>" + (String)sym + "</SYM><OP1>" +(String)re_expr + "</OP1></LOGIC>"; :}
           | rel_expr: re_expr {: RESULT = (String)re_expr; :};

rel_expr ::= add_sub_expr:as_expr rel_op:sym rel_expr:re_expr {: RESULT = "<REL><OP1>" + (String)as_expr + "</OP1><SYM>" + (String)sym + "</SYM><OP2>" + (String)re_expr + "</OP2></REL>"; :}
           | add_sub_expr:as_expr {: RESULT =  (String)as_expr ; :} ;

rel_op ::= ALABASTER:sym {: RESULT = "eq"; :}
         | BUSHY:sym {: RESULT = "ne"; :}
         | PEPPER:sym {: RESULT = "l"; :}
         | SUGARPLUM:sym {: RESULT = "g"; :}
         | WUNORSE:sym {: RESULT = "le"; :}
         | JINGLE:sym {: RESULT = "ge"; :};

add_sub_expr ::= mul_div_expr:md_expr add_sub_ops:sym add_sub_expr:as_expr {: RESULT = "<ARITH><OP1>" + (String)md_expr + "</OP1><SYM>"+ (String)sym + "</SYM><OP2>" + (String)as_expr + "</OP2></ARITH>";
                                                                            //Semantic.semanticExpr(symTabMgr, symbols, (String)RESULT, "int");
:}
               | mul_div_expr:md_expr {: RESULT = (String)md_expr; :};

add_sub_ops ::= DASHER:sym {: RESULT = (String)sym; :}
              | DANCER:sym {: RESULT = (String)sym; :};


mul_div_expr ::= exp_expr:ex_expr mul_div_ops:sym mul_div_expr:md_expr {: RESULT = "<ARITH><OP1>" + (String)ex_expr + "</OP1><SYM>" + (String)sym + "</SYM><OP2>" + (String)md_expr + "</OP2></ARITH>"; :}
               | exp_expr:ex_expr {: RESULT = (String)ex_expr; :};

mul_div_ops ::= PRANCER:sym {: RESULT = (String)sym; :}
              | VIXEN:sym {: RESULT = (String)sym; :}
              | COMET:sym {: RESULT = (String)sym; :};

exp_expr ::= un_arith_expr:un_expr RUDOLPH:sym exp_expr:ex_expr {: RESULT = "<ARITH><OP1>" +(String)un_expr + "</OP1><SYM>" + (String)sym + "</SYM><OP2><<" + (String)ex_expr + "</OP2></ARITH>"; :}
           | un_arith_expr:un_expr {: RESULT = (String)un_expr; :};

un_arith_expr ::= un_arith_ops:sym un_arith_expr:un_exp {: RESULT = "<ARITH><SYM>" + (String)sym + "</SYM><OP1>" + (String)un_exp + "</OP1></ARITH>"; :}
                | factor:fact {: RESULT = (String)fact; :};

un_arith_ops ::= GRINCH:sym {: RESULT = (String)sym; :}
               | QUIEN:sym {: RESULT = (String)sym; :};

factor ::= immutable:immut {: RESULT = (String)immut; :}
         | mutable:mut {: RESULT = (String)mut; :};

mutable ::= PERSONA:id {: RESULT = "<ID>" + (String)id + "</ID>"; :}
          | PERSONA:id ABREEMPAQUE expr:expr CIERRAEMPAQUE {: RESULT = "<ARR_ELEM><ID>" + (String)id + "</ID><EXPR>" + (String)expr + "</EXPR></ARR_ELEM>" ; :};

immutable ::= ABRECUENTO expr:expr CIERRACUENTO {: RESULT = expr; :}
            | call:call {: RESULT = "<CALL>" + (String)call + "</CALL>"; :}
            | literal:lit {: RESULT = "<LITERAL>" + (String)lit + "</LITERAL>"; :};

call ::= PERSONA:id ABRECUENTO args:args CIERRACUENTO {: RESULT = "<FUNC>" + (String)id + "</FUNC><ARGUMENTS>" + (String)args + "</ARGUMENTS>"; :};

args ::= arg_list:arg_l {: RESULT = (String)arg_l; :}
       | {: RESULT = ""; :};

arg_list ::= expr:expr PINO arg_list:arg_l {: RESULT = "<ARG>" + (String)expr + "</ARG>" + (String)arg_l; :}
           | expr:expr {: RESULT = "<ARG>" + (String)expr + "</ARG>";:};

literal ::= L_SANTACLAUS: l_char {: RESULT = "char=" + (String)l_char; :}
          | L_FATHERCHRISTMAS: l_int {: RESULT = "int=" + (String)l_int; :}
          | L_KRISKRINGLE: l_flt {: RESULT = "float=" + (String)l_flt; :}
          | L_SANNICOLAS: l_bool {: RESULT = "bool=" + (String)l_bool; :}
          | L_DEDMOROZ: l_str {: RESULT = "string=\"" + (String)l_str + "\""; :};